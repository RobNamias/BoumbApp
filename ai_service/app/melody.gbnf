root ::= MelodyObject
MelodyObject ::= "{" ws "\"sequence\"" ws ":" ws NoteList ws "}"

NoteList ::= "[" ws NoteItem ("," ws NoteItem)* ws "]"

NoteItem ::= "{" ws 
    "\"note\""     ws ":" ws NoteValue     ws "," ws
    "\"time\""     ws ":" ws TimeValue     ws "," ws
    "\"duration\"" ws ":" ws DurationValue ws "," ws
    "\"velocity\"" ws ":" ws VelocityValue ws
"}"

NoteValue ::= "\"" NoteName Octave "\""
NoteName ::= [A-G] | [A-G] "#"
Octave ::= [0-8]

TimeValue ::= "\"" Bar ":" Beat ":" Sixteenth "\""
Bar ::= [0-8] | [1-8] [0-8]
Beat ::= [0-3]
Sixteenth ::= [0-9] | "1" [0-5]

DurationValue ::= "\"" ("1n" | "2n" | "4n" | "8n" | "16n" | "32n") "\""
VelocityValue ::= "0." [1-9] | "1.0"

ws ::= [ \t\n]*
